%--------------------------------------------------------------------------------
% WHAT
%    Abstract Syntax for the Static Semantics of VDM++ and VDM-SL.
%      C preprocessor macroes:
%        VDMPP  -> VDM++ abstract syntax.
%        VDMSL  -> VDM-SL abstract syntax.
%        TPINFO -> Type information used by the code generator.
% $Id: common_as.vdm,v 1.74 2006/03/15 07:53:31 vdmtools Exp $
% Implemented in: ???
%--------------------------------------------------------------------------------

#define CNTXT cid :- CI`ContextId

#ifdef TPINFO
--#define ANDTI --and true -- CI`HasTypeInfo(n.cid)
#define ANDTI and CI`HasTypeInfo(n.cid)
--#define TPINV --inv n == true -- CI`HasTypeInfo(n.cid)
#define TPINV inv n == CI`HasTypeInfo(n.cid)
--#define TPPIDINV --inv n == true -- CI`HasTypeInfo(n.cid) and CI`HasPidMInfo(n.cid)
#define TPPIDINV inv n == CI`HasTypeInfo(n.cid) and CI`HasPidMInfo(n.cid)
#else
#define ANDTI
#define TPINV
#define TPPIDINV
#endif TPINFO

#ifdef VDMPP
\chapter{VDM++ Abstract Syntax}
#endif VDMPP

#ifdef VDMSL
\section{VDM-SL Abstract Syntax}
#endif VDMSL
\label{sec:as}

\begin{vdm_al}
module AS

imports
#ifdef JSS
  from JSSREP all,
#endif JSS
  from REP all,
  from CI all

exports all

definitions

types

VdmFiles = map CI`FileId to SpecFile;

SpecFile :: name      : [FileName]
            vdm       : Document
            tokenci   : CI`TokenContextInfo
            timestamp : [TimeStamp];

TimeStamp = token;

FileName = seq1 of char;

#ifdef VDMPP

-----------------------------------------------------------------------
---------------- Abstract Syntax Definitions for VDM++ ----------------
-----------------------------------------------------------------------

Document = seq1 of Class;

Class :: nm       : Name
         sys      : bool
         supercls : seq of Name
         defs     : [Definitions]
         useslib  : [TextLit]  -- useslib <> nil <=> we have a DLClass.
         CNTXT
inv cls == len cls.nm.ids = 1;

\end{vdm_al}

\subsection{Instance Variables}

\begin{vdm_al}
types

InstanceVarDef = InstAssignDef | InstanceInv;

InstAssignDef :: ad     : AssignDef
                 access : Access
                 stat   : bool
                 CNTXT
                 ;

InstanceInv :: expr   : Expr
               access : Access
               CNTXT
               ;
\end{vdm_al}

\subsection{Synchronisation Definitions}
\begin{vdm_al}
types

SyncDef = DeclarativeSync;

DeclarativeSync = Permission | Mutex;

Permission :: spec  : Name
              guard : Expr
              CNTXT
              ;

Mutex :: ops : [NameList]
         CNTXT
         ;

\end{vdm_al}

\subsection{Threads}
\begin{vdm_al}
types

ThreadDef = PerObl | SpoObl | Stmt;

PerObl :: period   : Expr
          jitter   : Expr
          delay    : Expr
          offset   : Expr
          mtd      : Name
          CNTXT
          ;

SpoObl :: delay    : Expr
          bound    : Expr
          offset   : Expr
          mtd      : Name
          CNTXT
          ;
#endif VDMPP

\end{vdm_al}

\subsection{Trace Definitions}

\begin{vdm_al}
types
TraceDefList = seq of TraceDefTerm;

TraceDefTerm = TraceDef | TraceDefAltn;

TraceDef = SimpleTrace | QualifiedTrace | RepeatTrace | QualifiedRepeatTrace;

TraceDefAltn :: term : TraceDefTerm
                tdef : TraceDef
                CNTXT
                ;

SimpleTrace = TraceApplyExpr | TraceBracketedExpr | TracePermuteExpr;

#ifdef VDMSL
TraceApplyExpr :: op   : Name
                  args : seq of Expr
                  CNTXT
                  ;
#endif VDMSL
#ifdef VDMPP
TraceApplyExpr :: obj  : [Name]
                  op   : Name
                  args : seq of Expr
                  CNTXT
                  ;
#endif VDMPP

TraceBracketedExpr :: list : TraceDefList
                      CNTXT
                      ;

TracePermuteExpr :: list : TraceDefList
                    CNTXT
                    ;

QualifiedTrace :: binds : seq1 of TraceBind
                  tdef  : SimpleTrace
                  CNTXT
                  ;

TraceBind = LocalTraceBind | LetTraceBind | LetBeTraceBind;

LocalTraceBind :: localdefs : seq1 of LocalDef
                  CNTXT
                  ;

LetTraceBind :: bind : BindList
                CNTXT
                ;

LetBeTraceBind :: bind   : BindList
                  stexpr : [Expr]
                  CNTXT
                  ;

RepeatTrace :: tdef : SimpleTrace
               pat  : TraceRepeatPattern
               CNTXT
               ;

TraceRepeatPattern = <ANY> | <ATLEASTONE> | <POSSIBLY> | RepeatFixed | RepeatInterval;

RepeatFixed :: num : NumLit
               CNTXT
               ;

RepeatInterval :: low  : NumLit
                  high : NumLit
                  CNTXT
                  ;

QualifiedRepeatTrace :: binds : seq1 of TraceBind
                        tdef  : SimpleTrace
                        pat   : TraceRepeatPattern
                        CNTXT
                        ;

\end{vdm_al}

\subsection{Document}

\begin{vdm_al}
#ifdef VDMSL
-----------------------------------------------------------------------
-------------------------- Document -----------------------------------
-----------------------------------------------------------------------

Document = seq of (Module | DLModule) | seq of Definitions
inv d == (exists x in set elems d & is_Definitions (x)) => (len d = 1) ;

-----------------------------------------------------------------------
--------------------------- Modules -----------------------------------
-----------------------------------------------------------------------

Module :: nm   : Name
          intf : Interface
          defs : [Definitions]
          CNTXT
          ;

Interface :: imp  : map Name to [ImportSig]
             exp  : [ExportSig]
             CNTXT
             ;

ImportSig :: tps : map Name to [TypeDef]
             val : map Name to [Type]
             fns : map Name to [(TypeVarList * FnType)]
             ops : map Name to [OpType]
             ren : map Name to Name
             CNTXT
             ;

ExportSig :: tps : map Name to [<STRUCT>]
             val : map Name to Type
             fns : map Name to (TypeVarList * FnType) --
             ops : map Name to OpType
             CNTXT
             ;

-----------------------------------------------------------------------
----------------- DL Modules ------------------------------
-----------------------------------------------------------------------

DLModule :: nm     : Name
            intf   : DLInterface
            useslib : [TextLit]
            CNTXT
            ;

DLInterface :: imp  : map Name to DLImportSig
               exp  : DLExportSig
               CNTXT
               ;

DLImportSig :: tps : map Name to [ImportSig]
inv imp == (forall impsig in set rng imp.tps & impsig = nil) ;

DLExportSig :: val : map Name to Type
               fns : map Name to FnType
               ops : map Name to OpType
               CNTXT
               ;
#endif VDMSL

-----------------------------------------------------------------------
---------------------- Access specifiers ------------------------------
-----------------------------------------------------------------------

Access = <DEFAULT_AS> | <PRIVATE_AS> | <PROTECTED_AS> | <PUBLIC_AS> | <NOT_INITIALISED_AS>;

-----------------------------------------------------------------------
-------------------------- Definitions --------------------------------
-----------------------------------------------------------------------

Definitions :: typem    : map Name to TypeDef
               valuem   : seq of ValueDef
               fnm      : map Name to FnDef
               opm      : map Name to OpDef
#ifdef VDMSL
               State    : [ StateDef ]
#endif VDMSL
#ifdef VDMPP
               instvars : seq of InstanceVarDef
               syncs    : seq of SyncDef
               threaddef : [ThreadDef]
#endif VDMPP
               tracem   : map NameList to TraceDefList
               CNTXT
               ;


StateDef :: tp   : CompositeType
            Inv  : [Invariant]  -- new. was: [ Pattern * Expr ]
            Init : [StateInit] -- new. was: [ Pattern * Expr ]
            CNTXT
            ;

StateInit ::
        pat  : Pattern
        expr : Expr
        CNTXT
        ;

\end{vdm_al}

The {\em StateDef} type definition is really not part of the VDM++ language, however,
the type definition is included in order to avoid too many ifdefs in the specification of the dynamic semantics.

\subsection{Types} \label{atypes}

\begin{vdm_al}
-----------------------------------------------------------------------
-------------------------- Type Definitions ---------------------------
-----------------------------------------------------------------------

TypeDef :: nm     : Name
           shape  : Type
           Inv    : [Invariant]
           Eq     : [Equal]
           Ord    : [Order]
           access : Access
           CNTXT
--inv td == true -- len td.nm.ids = 1
;

Invariant ::
     pat  : Pattern
     expr : Expr
     CNTXT
     ;

Equal ::
     lhs  : Pattern
     rhs  : Pattern
     expr : Expr
     CNTXT
     ;

Order ::
     lhs  : Pattern
     rhs  : Pattern
     expr : Expr
     CNTXT
     ;

Type = BasicType |
       QuoteType | CompositeType | UnionType |
       ProductType | OptionalType | SetType | SeqType |
       MapType | TypeName | FnType | BracketedType |
       OpType | TypeVar | VoidType | AllType;

BracketedType :: tp : Type
                 CNTXT
                 ;

BasicType = BooleanType | NumericType | TokenType | CharType;

BooleanType :: CNTXT
               ;

NumericType :: qtp : <NATONE> | <NAT> | <INTEGER> | <RAT> | <REAL>
               CNTXT
               ;

TokenType :: CNTXT
             ;

CharType :: CNTXT
            ;

QuoteType :: lit : QuoteLit
             CNTXT
             ;

CompositeType :: name : Name
                 fields : seq of Field
                 CNTXT
--inv ct == true -- len ct.name. ids = 1
;
\end{vdm_al}

The entry {\em dc} in the {\em Field} type describes if the field is an abstract field.

\begin{vdm_al}
Field :: sel  : [Name]
         type : Type
         dc   : bool
         CNTXT
--inv field == if field.sel <> nil
--             then len field.sel.ids = 1
--             else true;
inv field == field.sel <> nil => len field.sel.ids = 1;

UnionType :: tps : seq1 of Type
             CNTXT
             ;

ProductType :: tps : seq1 of Type
               CNTXT
               ;

OptionalType :: tp: Type
                CNTXT
                ;

SetType = Set0Type | Set1Type;

Set0Type :: elemtp : Type
           CNTXT
           ;

Set1Type :: elemtp : Type
           CNTXT
           ;

SeqType = Seq0Type | Seq1Type;

Seq0Type :: elemtp : Type
            CNTXT
            ;

Seq1Type :: elemtp : Type
            CNTXT
            ;

MapType = GeneralMap0Type | GeneralMap1Type | InjectiveMap0Type | InjectiveMap1Type;

GeneralMap0Type :: mapdom : Type
                  maprng : Type
                  CNTXT
                  ;

GeneralMap1Type :: mapdom : Type
                  maprng : Type
                  CNTXT
                  ;

InjectiveMap0Type :: mapdom : Type
                    maprng : Type
                    CNTXT
                    ;

InjectiveMap1Type :: mapdom : Type
                    maprng : Type
                    CNTXT
                    ;

TypeName :: name : Name
            CNTXT
            ;

FnType = PartialFnType | TotalFnType;

PartialFnType :: fndom : DiscretionaryType
                 fnrng : Type
                 CNTXT
                 ;

TotalFnType :: fndom : DiscretionaryType
               fnrng : Type
               CNTXT
               ;

DiscretionaryType = seq of Type;

TypeVar :: name : Name
           CNTXT
           ;

VoidType :: CNTXT
            ;

AllType :: CNTXT
           ;

-- AllType is used by the dynamic semantics to deal with the result type
-- of lambda expressions! Otherwise it is not used!!

\end{vdm_al}

\subsection{Functions} \label{afunctions}

\begin{vdm_al}

-----------------------------------------------------------------------
-------------------------- Functions Definitions ----------------------
-----------------------------------------------------------------------

FnDef = ExplFnDef | ImplFnDef | ExtExplFnDef;

ExplFnDef :: nm      : Name
             tpparms : TypeVarList
             tp      : FnType
             parms   : ParametersList
             body    : FnBody
             fnpre   : [Expr]
             fnpost  : [Expr]
             access  : Access
             stat    : bool
             measu   : [Expr | <NOTYETSPEC>]
             CNTXT
inv fndef == len fndef.nm.ids = 1;

ImplFnDef :: nm       : Name
             params   : TypeVarList
             partps   : ParameterTypes
             resnmtps : seq of NameType
             fnpre    : [Expr]
             fnpost   : Expr
             access   : Access
             stat     : bool
             CNTXT
inv fndef == len fndef.nm.ids = 1;

ExtExplFnDef :: nm       : Name
                params   : TypeVarList
                partps   : ParameterTypes
                resnmtps : seq of NameType
                body     : FnBody
                fnpre    : [Expr]
                fnpost   : [Expr]
                access   : Access
                stat     : bool
                measu   : [Expr]
                CNTXT
inv fndef == len fndef.nm.ids = 1;

TypeVarList = seq of TypeVar;        -- hc: not a node (no pos info about parenthesis
ParametersList = seq1 of Parameters; -- hc: not a node (no pos info about parenthesis
Parameters = seq of Pattern;         -- hc: not a node (no pos info about parenthesis
ParameterTypes = seq of PatTypePair; -- hc: not a node (no pos info about parenthesis

NameType :: nm : Name
            tp : Type
            CNTXT
inv nt == len nt.nm.ids = 1;

PatTypePair :: pats : seq1 of Pattern
               tp   : Type
               CNTXT
               ;

#ifdef VDMSL
NoBody = <NOTYETSPEC>;
#endif VDMSL
#ifdef VDMPP
NoBody = <NOTYETSPEC> | <SUBRESP>;
#endif VDMPP

FnBody :: body : Expr | NoBody
                 CNTXT
                 ;
\end{vdm_al}

\subsection{Operations}

\begin{vdm_al}
-----------------------------------------------------------------------
-------------------------- Operations Definitions ---------------------
-----------------------------------------------------------------------

OpDef = ExplOpDef | ImplOpDef | ExtExplOpDef;
\end{vdm_al}
#ifdef VDMPP
A constructor is an operation which (1) has the same name as the
current class name; (2) has no return type.
#endif //VDMPP
\begin{vdm_al}
ExplOpDef :: nm      : Name
             oppure  : bool
             opsync  : bool
             tp      : OpType
             parms   : Parameters
             body    : OpBody
             oppre   : [Expr]
             oppost  : [Expr]
             access  : Access
             stat    : bool
             constr  : bool
             CNTXT
             ;

\end{vdm_al}
The field $timepost$ in $ExplOpDef$ will always be {\sf nil}
in VDM-SL specifications.
\begin{vdm_al}

ImplOpDef :: nm       : Name
             oppure   : bool
             opsync   : bool
             partps   : ParameterTypes
             resnmtps : seq of NameType
             opext    : seq of ExtInf
             oppre    : [Expr]
             oppost   : Expr
             excps    : seq of Error
             access   : Access
             stat     : bool
             constr   : bool
             CNTXT
             ;

\end{vdm_al}
\begin{vdm_al}

ExtExplOpDef :: nm       : Name
                oppure   : bool
                opsync   : bool
                partps   : ParameterTypes
                resnmtps : seq of NameType
                body     : OpBody
                opext    : seq of ExtInf
                oppre    : [Expr]
                oppost   : [Expr]
                excps    : seq of Error
                access   : Access
                stat     : bool
                constr   : bool
                CNTXT
                ;

\end{vdm_al}
The field $timepost$ in $ExtExplOpDef$ will always be {\sf nil}
in VDM-SL specifications.
\begin{vdm_al}

OpType :: opdom : DiscretionaryType
          oprng : Type
          CNTXT
          ;

ExtInf :: mode : Mode
          vars : seq of Name
          tp   : [Type]
          CNTXT
          ;

Mode = <READ> | <READWRITE>;

OpBody :: body : Stmt | NoBody
                 CNTXT
                 ;
\end{vdm_al}

\subsection{Values} \label{avalues}

\begin{vdm_al}
-----------------------------------------------------------------------
-------------------------- Value Definitions --------------------------
-----------------------------------------------------------------------

ValueDef :: pat    : Pattern
            tp     : [Type]
            val    : Expr
            access : Access
            stat   : bool
            CNTXT
            ;
\end{vdm_al}

\subsection{Expressions} \label{aexpr}
\begin{vdm_al}

-----------------------------------------------------------------------
-------------------------- Expressionts -------------------------------
-----------------------------------------------------------------------

Expr = BracketedExpr | DefExpr | LetExpr | LetBeSTExpr| IfExpr | CasesExpr |
       UnaryExpr | BinaryExpr | QuantExpr| IotaExpr |
       SetEnumerationExpr | SetComprehensionExpr | SetRangeExpr |
       SeqEnumerationExpr | SeqComprehensionExpr |
       SubSequenceExpr | SeqModifyMapOverrideExpr |
       MapEnumerationExpr | MapComprehensionExpr | Maplet |
       TupleConstructorExpr |
       RecordConstructorExpr | RecordModifierExpr |
       FieldSelectExpr | ApplyExpr |
       IsExpr | Literal | Name | OldName | UndefinedExpr |
       NarrowExpr |
       TupleSelectExpr | TypeJudgementExpr | PreConditionApplyExpr |
#ifdef VDMPP
       SelfExpr | NewExpr | IsOfClassExpr | IsOfBaseClassExpr |
       SameBaseClassExpr | SameClassExpr |
       ActExpr | FinExpr | ActiveExpr | WaitingExpr | ReqExpr |
       ThreadIdExpr | GuardExpr |                                      -- (!) OlOpp
#ifdef VICE
       CurTimeExpr |
#endif VICE
#endif
       TokenConstructorExpr | FctTypeInstExpr | LambdaExpr |
       LastRes | Macro;

-- only used in the concurrency module                                 -- (!) OlOpp
                                                                       -- (!) OlOpp
GuardExpr :: expr : AS`Expr  -- this is the original guard expression  -- (!) OlOpp
             name : AS`Name; -- this is the name of the class,         -- (!) OlOpp
                             --  in which the expression was defined.  -- (!) OlOpp

BracketedExpr :: expr  : Expr
                 CNTXT
TPINV
                 ;

DefExpr :: Def : seq1 of (PatternBind * Expr)
           In  : Expr
           CNTXT
TPINV
           ;

LetExpr :: localdef : seq1 of LocalDef
           body     : Expr
           CNTXT
TPINV
           ;

LocalDef = FnDef | ValueDef;

LetBeSTExpr :: lhs : BindList
               St  : [Expr]
               In  : Expr
               CNTXT
TPINV
               ;

IfExpr :: test   : Expr
          cons   : Expr
          elsif  : seq of ElseifExpr
          altn   : Expr
          CNTXT
TPINV
;

ElseifExpr :: test : Expr
              cons : Expr
              CNTXT
              ;

CasesExpr :: sel    : Expr
             altns  : seq1 of CaseAltn
             Others : [ Expr ]
             CNTXT
TPINV
;

CaseAltn :: match : seq1 of Pattern
            body  : Expr
            CNTXT
            ;

UnaryExpr = PrefixExpr;

PrefixExpr :: opr   : UnaryOp
              arg   : Expr
              CNTXT
TPINV
;

UnaryOp = <NUMPLUS> | <NUMMINUS> | <NUMABS> | <FLOOR> |
          <NOT> |
          <SETCARD> | <SETDISTRUNION> | <SETDISTRINTERSECT> | <SETPOWER> |
          <SEQLEN> | <SEQDISTRCONC> | <SEQELEMS> | <SEQINDICES> |
          <SEQTAIL> | <SEQHEAD> | <SEQREVERSE> |
          <MAPDOM> | <MAPRNG> | <MAPDISTRMERGE> | <MAPINVERSE>;

BinaryExpr :: left  : Expr
              opr   : BinaryOp
              right : Expr
              CNTXT
TPINV
;

BinaryOp = <NUMPLUS> | <NUMMINUS> | <NUMMULT> | <NUMDIV> |
           <NUMREM> | <NUMMOD> | <NUMEXP> | <INTDIV> |
           <NUMLT> | <NUMLE> | <NUMGT> | <NUMGE> |
           <AND> | <OR> | <IMPLY> | <EQUIV> |
           <EQ> | <NE> |
           <SETUNION> | <SETINTERSECT> | <SETMINUS> | <SUBSET> |
           <PROPERSUBSET> | <INSET> | <NOTINSET> |
           <SEQCONC> |
           <MAPMERGE> | <MAPDOMRESTTO> | <MAPDOMRESTBY> |
           <MAPRNGRESTTO> | <MAPRNGRESTBY> |
--           <COMPOSE>;
           <COMPOSE> | ( <LEXORD> * nat );

QuantExpr = AllOrExistsExpr | ExistsUniqueExpr;

AllOrExistsExpr :: quant : AllOrExistsQuantifier
                   bind  : BindList
                   pred  : Expr
                   CNTXT
TPPIDINV
                   ;

ExistsUniqueExpr :: bind : Bind
                    pred : Expr
                    CNTXT
TPPIDINV
                    ;

AllOrExistsQuantifier = <ALL> | <EXISTS>;

IotaExpr :: bind : Bind
            pred : Expr
            CNTXT
TPINV
;

SetEnumerationExpr :: els : seq of Expr
                      CNTXT
TPINV
;

SetComprehensionExpr :: elem : Expr
                        bind : BindList
                        pred : [Expr]
                        CNTXT
TPPIDINV
                        ;

SetRangeExpr :: lb : Expr
                ub : Expr
                CNTXT
TPINV
                ;

SeqEnumerationExpr :: els : seq of Expr
                      CNTXT
TPINV
;

SeqComprehensionExpr :: elem : Expr
                        bind : SetBind | SeqBind
                        pred : [Expr]
                        CNTXT
TPPIDINV
;

SubSequenceExpr :: sequence : Expr
                   frompos  : Expr
                   topos    : Expr
                   CNTXT
TPINV
;

SeqModifyMapOverrideExpr :: seqmap : Expr
                            mapexp : Expr
                            CNTXT
TPINV
;

MapEnumerationExpr :: els : seq of Maplet
                      CNTXT
TPINV
;

Maplet :: mapdom : Expr
          maprng : Expr
          CNTXT
;

MapComprehensionExpr :: elem : Maplet
                        bind : BindList
                        pred : [Expr]
                        CNTXT
TPPIDINV
;

TupleConstructorExpr :: fields : seq1 of Expr
                        CNTXT
TPINV
;

TokenConstructorExpr :: field : Expr
                        CNTXT
TPINV
;

RecordConstructorExpr :: tag: Name
                         fields: seq of Expr
                         CNTXT
TPINV
;

RecordModifierExpr :: rec : Expr
                      modifiers : seq1 of RecordModification
                      CNTXT
TPINV
;

RecordModification :: field   : Name
                      newexpr : Expr
                      CNTXT
inv rm == len rm.field.ids = 1;

#ifdef VDMSL
FieldSelectExpr :: rec : Expr
                   nm  : Name
                   CNTXT
inv n == len n.nm.ids = 1
#endif VDMSL
#ifdef VDMPP
FieldSelectExpr :: rec : Expr
                   nm  : Name | FctTypeInstExpr
                   CNTXT
inv n == ((is_Name (n.nm) and len n.nm.ids <= 2) or
          (is_FctTypeInstExpr (n.nm) and len n.nm.polyfct.ids <= 2))
#endif VDMPP
           ANDTI
;
\end{vdm_al}

\begin{vdm_al}
FctTypeInstExpr :: polyfct : Name
                   inst    : seq1 of Type
                   CNTXT  -- tp_info
TPINV
;

LambdaExpr :: parm : seq of TypeBind
              body : Expr
              type : [ Type ]
              CNTXT
TPINV
;

ApplyExpr :: fct : Expr
             arg : seq of Expr
             CNTXT
TPINV
;

IsExpr :: type : BasicType | Name
          arg  : Expr
          CNTXT
TPINV
;

UndefinedExpr::
          CNTXT
;

NarrowExpr :: expr : Expr
              type : Type
              CNTXT
TPINV
;

TupleSelectExpr :: tuple : Expr
                   no    : NumLit | RealLit
                   CNTXT
;

TypeJudgementExpr :: expr : Expr
                     type : Type
                     CNTXT
;

PreConditionApplyExpr :: fct : Expr
                         arg : seq of Expr
                         CNTXT
;

#ifdef VDMPP
SelfExpr ::
        CNTXT
TPINV
;

ThreadIdExpr ::
        CNTXT
;

#ifdef VICE
CurTimeExpr ::
        CNTXT
;
#endif VICE

NewExpr :: cls : Name
           args : seq of Expr
           CNTXT
inv ns == len ns.cls.ids = 1;

IsOfClassExpr :: cls : Name
                 arg : Expr
                 CNTXT
inv n == len n.cls.ids = 1 ANDTI
;

IsOfBaseClassExpr :: cls : Name
                     arg : Expr
                     CNTXT
inv n == len n.cls.ids = 1 ANDTI
;

SameBaseClassExpr :: expr1 : Expr
                     expr2 : Expr
                     CNTXT
TPINV
;

SameClassExpr :: expr1 : Expr
                 expr2 : Expr
                 CNTXT
TPINV
;

ActExpr :: mthd : NameList
           CNTXT
;

FinExpr :: mthd : NameList
           CNTXT
;

ActiveExpr :: mthd : NameList
              CNTXT
;

WaitingExpr :: mthd : NameList
               CNTXT
;

ReqExpr :: mthd : NameList
           CNTXT
;
#endif VDMPP

\end{vdm_al}

\subsection{Names} \label{anames}
\begin{vdm_al}
Name :: ids : Ids
        CNTXT
TPINV
;

Ids = seq of Id;

Id = seq of char;

NameList = seq1 of Name;

OldName :: ids : Ids
           CNTXT
TPINV
;

\end{vdm_al}

\subsection{Statements} \label{astmt}
\begin{vdm_al}

-----------------------------------------------------------------------
-------------------------- Statements ---------------------------------
-----------------------------------------------------------------------

Stmt = DefStmt | LetStmt | LetBeSTStmt|
       AssignStmt | SeqForLoopStmt | AtomicAssignStmt |
       SetForLoopStmt | IndexForLoopStmt | WhileLoopStmt |
       ReturnStmt | IfStmt |
       CasesStmt | BlockStmt | IdentStmt |
       ErrorStmt | AlwaysStmt | CallStmt | SpecificationStmt |
       AssertStmt |
#ifdef VDMPP
       StartStmt | StartListStmt | StopStmt | StopListStmt |
#ifdef VICE
       DurationStmt | CycleStmt |
#endif VICE
#endif VDMPP
       ExitStmt | TrapStmt | RecTrapStmt | NonDetStmt ;

DefStmt :: value : seq1 of (PatternBind * Expr)
           In    : Stmt
           CNTXT
           ;

LetStmt :: localdef : seq1 of LocalDef
           In       : Stmt
           CNTXT
           ;

LetBeSTStmt :: bind : BindList
               St   : [Expr]
               In   : Stmt
               CNTXT
               ;

BlockStmt :: dcls  : seq of AssignDef
             stmts : seq of Stmt
             CNTXT
             ;

AssignDef :: var     : Name
             tp      : Type
             dclinit : [Expr]
             CNTXT
             ;

AtomicAssignStmt :: atm : seq1 of AssignStmt
                    CNTXT
                    ;

AssignStmt :: lhs : StateDesignator
              rhs : Expr
              CNTXT
              ;

#ifdef VDMSL
StateDesignator = Name | FieldRef | MapOrSeqRef | NarrowRef;
#endif VDMSL
#ifdef VDMPP
StateDesignator = Name | FieldRef | MapOrSeqRef | NarrowRef | SelfExpr;
#endif VDMPP

FieldRef :: var : StateDesignator
            sel : Name
            CNTXT
            ;

MapOrSeqRef :: var : StateDesignator
               arg : Expr
               CNTXT
               ;

NarrowRef :: var : StateDesignator
             type : Type
             CNTXT
             ;

SeqForLoopStmt :: cv  : PatternBind
                  fseq : Expr
                  body : Stmt
                  CNTXT
                  ;

SetForLoopStmt :: cv   : Pattern
                  fset : Expr
                  body : Stmt
                  CNTXT
                  ;

IndexForLoopStmt :: cv   : Name
                    lb   : Expr
                    ub   : Expr
                    By   : [Expr]
                    body : Stmt
                    CNTXT
inv ifls == len ifls.cv.ids = 1;

WhileLoopStmt :: test : Expr
                 body : Stmt
                 CNTXT
                 ;

ReturnStmt :: val : [Expr]
              CNTXT
              ;

IfStmt :: test   : Expr
          cons   : Stmt
          elsif  : seq of ElseifStmt
          altn   : [Stmt]
          CNTXT
          ;

ElseifStmt :: test : Expr
              cons : Stmt
              CNTXT
              ;

Error :: nm     : Name
         cond   : Expr
         action : Expr
         CNTXT
         ;

ErrorStmt ::
          CNTXT
          ;

AlwaysStmt :: Post : Stmt
              body : Stmt
              CNTXT
              ;

TrapStmt :: pat : PatternBind
            Post : Stmt
            body : Stmt
            CNTXT
            ;

RecTrapStmt :: traps : seq1 of Trap
               body  : Stmt
               CNTXT
               ;

Trap :: match    : PatternBind
        trappost : Stmt
        CNTXT
        ;

ExitStmt :: expr : [Expr]
            CNTXT
            ;

NonDetStmt :: stmts : seq1 of Stmt
              CNTXT
              ;

CallStmt :: obj    : [Expr]
            oprt   : Name
            args   : seq of Expr
            CNTXT
TPINV
;

CasesStmt :: sel    : Expr
             altns  : seq1 of CasesStmtAltn
             Others : [Stmt]
             CNTXT
             ;

CasesStmtAltn :: match : seq1 of Pattern
                 body  : Stmt
                 CNTXT
                 ;

IdentStmt ::
           CNTXT
           ;

SpecificationStmt :: opext    : seq of ExtInf
                     oppre    : [Expr]
                     oppost   : Expr
                     excps    : seq of Error
                     CNTXT
                     ;

AssertStmt :: expr : Expr
              CNTXT
              ;

\end{vdm_al}
#ifdef VDMPP
\begin{vdm_al}
StartStmt :: expr : Expr
             CNTXT
             ;

StartListStmt :: expr : Expr
                 CNTXT
                 ;

StopStmt :: expr : Expr
            CNTXT
            ;

StopListStmt :: expr : Expr
                CNTXT
                ;

\end{vdm_al}

#endif VDMPP

#ifdef VICE
\begin{vdm_al}
DurationStmt :: num  : Expr -- RealLit
                stmt : Stmt
                CNTXT
                ;
\end{vdm_al}

CycleStmt is similar to DurationStmt except that it depends upon the number
of cycles that must be executed rather than a fixed duration.

\begin{vdm_al}
CycleStmt :: num   : Expr -- RealLit
             stmt  : Stmt
             CNTXT
             ;
\end{vdm_al}
#endif VICE

\subsection{Patterns} \label{apattern}
\begin{vdm_al}

-----------------------------------------------------------------------
-------------------- Patterns and Bindings ----------------------------
-----------------------------------------------------------------------

Pattern = PatternName | MatchVal | SetPattern | SeqPattern | MapPattern |
          TuplePattern | RecordPattern | MapletPattern
#ifdef VDMPP
          | ObjectPattern | FieldPattern
#endif VDMPP
          ;

PatternName :: nm : [Name] | OldName
               tp : [Type]
               CNTXT
TPINV
;
\end{vdm_al}

The $OldName$ type in $PatternName$ is used by the code generator, in
order to code generate post conditions on operations. Note, that old
names can not appear in pattern names in the concrete syntax.

\begin{vdm_al}
MatchVal :: val : Expr
            CNTXT
            ;

SetPattern = SetEnumPattern | SetUnionPattern;
\end{vdm_al}

The empty set is represented using $MatchVal$ and not
$SetEnumPattern$. Consequently $SetEnumPattern$ contains
$\seqof+{Pattern}$ instead of $\seqof*{Pattern}$.

\begin{vdm_al}
SetEnumPattern :: Elems : seq1 of Pattern
                  CNTXT
TPINV
;

SetUnionPattern :: lp : Pattern
                   rp : Pattern
                   CNTXT
                   ;

SeqPattern = SeqEnumPattern | SeqConcPattern;
\end{vdm_al}

The empty sequence is represented using $MatchVal$ and not
$SeqEnumPattern$. Consequently $SeqEnumPattern$ contains
$\seqof+{Pattern}$ instead of $\seqof*{Pattern}$.

\begin{vdm_al}
SeqEnumPattern :: els : seq1 of Pattern
                  CNTXT
TPINV
;

SeqConcPattern :: lp : Pattern
                  rp : Pattern
                  CNTXT
                  ;

MapPattern = MapEnumPattern | MapMergePattern;

MapletPattern :: dp : Pattern
                 rp : Pattern
                 CNTXT
                 ;

MapEnumPattern :: mls : seq1 of MapletPattern
                  CNTXT
                  ;

MapMergePattern :: lp : Pattern
                   rp : Pattern
                   CNTXT
                   ;

#ifdef VDMPP
ObjectPattern :: cls : Name
                 fields : seq of FieldPattern
                 CNTXT
                 ;

FieldPattern :: nm : Name
                pat : Pattern
                CNTXT
                ;
#endif VDMPP

TuplePattern :: fields : seq1 of Pattern
                CNTXT
TPINV
;

RecordPattern :: nm: Name
                 fields: seq of Pattern
                 CNTXT
TPINV
\end{vdm_al}

\subsection{Bindings} \label{abinding}
\begin{vdm_al}
types

PatternBind = Pattern | Bind;

Bind = SetBind | TypeBind;

SetBind :: pat : Pattern
           Set : Expr
           CNTXT
           ;

SeqBind :: pat : Pattern
           Seq : Expr
           CNTXT
           ;

TypeBind :: pat : Pattern
            tp  : Type
            CNTXT
            ;

BindList = seq of MultBind;

MultBind = MultSetBind | MultTypeBind;

MultSetBind :: pat : seq1 of Pattern
               Set : Expr
               CNTXT
               ;

MultTypeBind :: pat : seq1 of Pattern
                tp  : Type
                CNTXT
                ;
\end{vdm_al}

\subsection{Literals} \label{aliterals}
\begin{vdm_al}

-----------------------------------------------------------------------
-------------------- Literals -----------------------------------------
-----------------------------------------------------------------------

Literal = BoolLit | NilLit | NumLit | RealLit | CharLit | TextLit | QuoteLit;

BoolLit :: val : bool
           CNTXT
           ;

NilLit ::
        CNTXT
        ;

RealLit :: val : real
           CNTXT
           ;

--NumLit = RealLit
NumLit :: val : real
          CNTXT
inv n == is_int(n.val);

CharLit :: val : char
           CNTXT
           ;

TextLit :: val : seq of char
           CNTXT
           ;

QuoteLit :: val : seq1 of char
            CNTXT;
\end{vdm_al}

\begin{vdm_al}
MacroTp= <MACRO_FILE> | <MACRO_LINE> | <MACRO_COLUMN> | <MACRO_CLMOD> | <MACRO_FNOP>;

Macro :: tp : MacroTp
         CNTXT;
\end{vdm_al}

\subsection{Structure combining the AST and the ContextInfo} \label{AstCI}

{\em AstCI} is a structure that is not directly part of the AST. It
combines a {\em Document} or {\em Expr}, a {\em CI`TokenContextInfo\/}
and a {\em CI` FileIdMap} into a compound structure that is used by
all the test environments.

\begin{vdm_al}

-----------------------------------------------------------------------
-------------------- AstCI --------------------------------------------
-----------------------------------------------------------------------

AstCI :: ast   : Document | Expr
         tllci : seq of CI`TokenContextInfo
         fidm  : CI` FileIdMap;
\end{vdm_al}

\subsection{Debugger Constructs} \label{debugconstr}

{\em \/} is not directly part of the AST but is so tightly
related to it that is has been defined as an AST node anyway. It is
the node corresponding to the \$\$ command in the debugger.

\begin{vdm_al}

-----------------------------------------------------------------------
-------------------- Debugger Constructs ------------------------------
-----------------------------------------------------------------------

LastRes ::
        CNTXT
;

end AS
\end{vdm_al}

